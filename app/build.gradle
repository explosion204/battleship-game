apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-kapt'

def getVersionCode = { name ->
    Integer code = 0
    name = name.replaceAll('v', '')
    String[] components = name.split("\\.")
    Integer i = components.size() - 1

    for (component in components) {
        code += Math.pow(1000, i) * (component as Integer)
        i--;
    }

    return code
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--tags', '--long'
        standardOutput = stdout
    }

    stdout = stdout.toString().trim()
    stdout = stdout.replaceAll(stdout.charAt(stdout.indexOf('-')).toString(), '.')

    return stdout[0..(stdout.lastIndexOf('.') - 1)]
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.explosion204.battleship"
        minSdkVersion 23
        targetSdkVersion 30
        versionName getVersionName()
        versionCode getVersionCode(versionName)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.wear:wear:1.0.0'
    implementation 'com.google.firebase:firebase-database:19.5.1'
    compileOnly 'com.google.android.wearable:wearable:2.5.0'
    def nav_version = "2.3.1"
    def material_version = "1.2.1"
    def dagger_version = "2.29.1"
    def lifecycle_version = "2.2.0"
    def picasso_version = "2.71828"

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.firebase:firebase-auth:19.3.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.2.1'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-firestore:22.0.0'
    implementation 'com.google.firebase:firebase-storage:19.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"

    implementation 'androidx.cardview:cardview:1.0.0'

    // Material Design
    implementation "com.google.android.material:material:$material_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Dagger Android
    api "com.google.dagger:dagger-android:$dagger_version"
    api "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

    // Picasso
    implementation "com.squareup.picasso:picasso:$picasso_version"
}
